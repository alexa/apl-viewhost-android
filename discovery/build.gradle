/*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

ext {
    aplAndroidCmakeArgs = " -DCMAKE_VERBOSE_MAKEFILE=ON"
    aplCoreDirCmakeArg = "-DAPL_CORE_DIR=" + projectDir + "/../../apl-core-library"
    if (project.hasProperty('aplCoreDir')) {
        aplCoreDirCmakeArg = "-DAPL_CORE_DIR=" + aplCoreDir
    } else {

    }
}

android {
    compileSdkVersion 28
    ndkVersion "23.0.7599858"
    buildToolsVersion "30.0.2"


    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
        externalNativeBuild {
            cmake {
                // Sets optional flags for the C++ compiler.
                // Enables RTTI (RunTime Type Information) support and C++ exceptions.
                cppFlags "-std=c++11", "-frtti", "-fexceptions", "-DBUILD_ALEXAEXTENSIONS=On", "-DALEXAEXTENSIONS=1"
                // Build the APL Core JNI library (excludes all other targets)
                targets "alexaext", "discovery-jni"
                // Enable APL Core JNI build, and be verbose.
                arguments aplCoreDirCmakeArg, aplAndroidCmakeArgs
            }
            ndk {
                // Specifies the ABI configurations for the native libraries
                // that Gradle will build and package with the APK.
                abiFilters 'x86', 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            version "3.10.2"
            path "CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":common")

    implementation 'androidx.appcompat:appcompat:1.0.0'

    implementation 'com.android.support:support-annotations:28.0.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'org.mockito:mockito-core:2.25.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker:2.25.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.25.0'

}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                pom {
                    description = 'Commits: APLViewhostAndroid=' + System.env.CODEBUILD_RESOLVED_SOURCE_VERSION +
                            ',APLCoreEngine=' + System.env.CORE_SOURCE_VERSION
                }
            }
        }
    }
}

task release(type: Copy, dependsOn: ['build', 'publish']) {
    from 'build/outputs/aar'
    into '../build/discovery'
}